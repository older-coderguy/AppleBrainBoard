;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; $Id: Worksv8.asm 1.0 12/22/2025  12:28PM 
; Author: Leslie Newman
;
; Load DOS 3.3 into Apple II+ ram on boot.
;
; Hot keys-
; Control M = Load DOS, enter the monitor.
; Control C = load DOS, catalog disk.
; Control B = Load DOS, display prompt.
;
; Telemark Assembler (TASM) version 3.2
; Using an old dos program Bintel.com to convert hex file generated by tasm
; to the correct format hex and binary files that my eprom programmer can use.
;
;
;REVISION: 1-3-2026
; ADD TO THE PROGRAM LOADED AT $1000 TO CALL $FB60 THAT PRINTS
; APPLE ][.
;
;REVISION: 1-19-2026
; FIXED HOT KEYS SO THAT CATALOG AND MONITOR ENTRY WORKS.
;
;REVISION: 1-29-2026
; Cleaned up this assembly file, removing assembly code that I had remarked out.
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; 
; RESET ROUTINE FOR BRAIN BOARD running in II+
; Processor: 6502
;
; THIS CODE SET FOR BRAIN BOARD IN SLOT 4
; This code must be loaded at $1000 in the EPROM. That works out as
; $D000 in the Brain Board. Make sure that the F8 ROM reset vestor at FFFC-FFFD
; Points to $D800 on power up or reset of the Apple II.
;
; THESE ARE HANDY DEFINES FOR THE COMPILER
;
#define EQU     .EQU
#define ORG     .ORG
#define RMB     .BLOCK
#define FCB     .BYTE
#define FCC     .TEXT
#define FDB     .WORD
#define	DW	.WORD	;define word
#define DFB	.BYTE	;define byte
#define equ     .EQU
#define org     .ORG
#define rmb     .BLOCK
#define fcb     .BYTE
#define fcc     .TEXT
#define fdb     .WORD
#define dfb	.BYTE
#define	END	.END
#define HOFFLOW	#$C1	;This is the slot number of Brain Board
			;low byte $C1 = Slot 4
			;low byte $D1 = Slot 5
			;using #define to assign the variable name to a HEX value
;
;
; These equates are used by the Move Dos routine
;
SRCE		EQU	$6
SRCEHI		EQU	$7
DSTN		EQU	$8
DSTNHI		EQU	$9
MOVEP3		EQU	$9E25
RTSTEMP		EQU	$9E41

;
; These equates are used by the main Brain Board initialization code
;
COUNT		equ	$300	;USE AS A COUNTER WHEN NEEDED
INVFLG		EQU	$32	;SET TO $FF FOR NORMAL VIDEO
PROMPT		EQU	$33
CSWL		EQU	$36
A1L		EQU	$3C
A1H		EQU	$3D
A2L		EQU	$3E
A2H		EQU	$3F
A4L		EQU	$42
A4H		EQU	$43
BFFRFLG		EQU	$45
ONERR		EQU	$D8
DOSWARM		EQU	$3D0
DOSWRMHI	EQU	$3D
DOSWRMLO	EQU	$00
PWRUP		EQU	$3F4	;THIS IS PWREDUP HOLDS RANDOM VALUE IF A POWER UP
SAVEPWRUP	EQU	$2000	;THIS IS PWREDUP HOLDS RANDOM VALUE IF A POWER UP
ALLMENUS	EQU	$2100
CATALOG		EQU	$A56E
CATALOGHI	EQU	$A5	;Catalog disk
CATALOGLO	EQU	$6E	;Catalog disk
DOSCSWL		EQU	$AA53	;DOS output hook. Default is $FDF0
DOSCMDX		EQU	$AA5F	;DOS 3.2/3.3 Command number
DOSSTRT		EQU	$B73A	;
READKEY		EQU	$C000	;Peek to read keyboard. Value > 127 means a key has been pressed.
KEYSTROBE	EQU	$C010	;Keyboard strobe. WRITE TO CLEAR STROBE.
SAVEKEY		EQU	$2001	;SAVE THE LAST KEY PRESSED
SPEAKER		EQU	$C030	;ONLY READ LOCATION $C030. SATHER DID AN STA IN HIS ORIGINAL CODE
AN0OFF		EQU	$C058	;Poke 0 to Clear game AN0
AN1OFF		EQU	$C05A	;Poke 0 to Clear game AN1
OPENAPL		EQU	$C061	;*** NOT IN II+
SLDAPL		EQU	$C062	;*** NOT IN II+
RDONLY		EQU	$C080	;Write PROTECT RAM. Select 2nd bank of $D000 - $DFFF RAM in language card.
				;$C084 does the same as $C080.
WRTONLY		EQU	$C081	;Write ENABLE RAM. in II+ DOS 3.2 Read_Write track sector and also 
				;Read-Deselect 2nd bank of $D000 - $DFFF RAM in language card. 
HRAMOFF		EQU	$C082	;Write PROTECT RAM. in II+ DOS 3.2 Read_Write track sector and also
				;Read-Deselect 2nd bank of $D000 - $DFFF RAM in language card.
;HOSSOFF	EQU	$C0D1	;EXIT THE Brain Board ROM to Motherboard for slot 5
;BANK1		EQU	$C0D2	;This switched to the upper bank of ROM ** no longer used by my code
HOSSOFF		EQU	$C0C1	;EXIT THE Brain Board to Motherboard FOR SLOT 4 !! CURRENTLY NOT USED
BANK1		EQU	$C0C2	;This switched to the upper bank of ROM ** no longer used by my code
CLRROM		EQU	$CFFF	;Turn off Flip Flops. Disable expansion ROM
BASIC		EQU	$DFFF	;*** Looks like garbage in the II+.
NRESET		EQU	$FA62	;Normal Reset Vector for II+ (IIe is $FA61)
INIT		EQU	$FB2F	;Screen init. Reset Text mode
TEXTMODE	EQU	$FB39	;TEXT MODE JUST LIKE BASIC
GOTOCX		EQU	$FBB4	;*** Monitor memory location MD3.In new F8 ROM code here saves ROM states
HOME		EQU	$FC58	;HOME. Clear screen. Cursor top left
WAIT		EQU	$FCA8	;Call Wait loop. Set A register with calculation value
MOVE		EQU	$FE2C	;monitor memory move
SETNORM		EQU	$FE84	;Set video output to normal
MONITOR		EQU	$FF58	;JSR here to find out where one is. Sets Overflow flag.
NMIDSTN		EQU	$FFFA	;Address of NMI (Non-Maskable interupt) vector
;
;
; 
;
;  BANK 0 ROUTINES
;
;  
;
; ** Note: I did not include Integer Basic. There was no room in the Brain Board
;          to fit Integer and Dos 3.3. All I really wanted was to have DOS load
;          instantly on power up. An idea for the future is to have a assembly routine
;	   do some bank switching and load the Integer Basic from the upper ROM area.
;	   I think this would work.
;
; HOT KEYS-
;
; CONTROL + C WILL CATALOG THE DISK
; CONTROL + M WILL ENTER THE MONITOR
; CONTROL + B LOADS DOS AGAIN AFTER INITIAL BOOT
;
;
		.ORG	$D000
		JMP	_MENUS
;
;
; Some future menus that can be displayed on power up.
;
MENU_1	.TEXT	"DOS 3.3 HAS BEEN LOADED!\n"
MENU_2	.TEXT	"CONTROL + C = CATALOG DISK.\n"
MENU_3	.TEXT	"CONTROL + M = ENTER MONITOR.\n"
MENU_4	.TEXT	"CONTROL + B = RELOAD DOS.\n"
;
;
;**********************************************************
;
;	BEGIN RESET HANDLER
; RESET VECTOR IS FFFC AND FFFD. THIS CODE BEGINS AT $D000.
; ON POWER UP, THE F8 ROM RESET VECTOR HAS FFFC-FFFD LOADED WITH D000
; SO THE CPU VECTORS TO THIS PROGRAM. ONCE DOS HAS BEEN LOADED INTO THE APPLE RAM
; THIS PROGRAM PLACES A SHORT ASSEMBLER ROUTINE AT $1000 IN THE APPLE MEMORY. THIS ROUTUINE
; TURNS OFF THE BRAIN BOARD AND THEN DOES SOME RESET STUFF. IT ALSO CHECKS
; FOR THE HOT KEYS TO CATALOG DISK OR GO INTO THE MONITOR.
; THIS ROUTINE CHANGES HOW SATHER DID THE EXIT. HIS DOSHOSS WAS EXPECTING AN RTS
; TO BE AT A MOTHERBOARD ROM LOCATION AFTER THE DOSHOSS WAS TURNED OFF. IT SEEMS MORE
; FLEXIBLE TO HAVE A PROGRAM RUNNING IN THE APPLE MEMORY THAT TURNS OFF THE BRAIN BOARD AND
; CAN THEN CLEAN UP REGISTERS, CLEAR SCREEN AND COMPLETE THE BOOT.
;
;  
; MOVE THE STRINGS INTO MEMORY AT $2100
;
;
_MENUS		LDX	#$FF	;USE X AS COUNTER. SHOULD NEVER REACH $FF COUNT.
		LDY	#$00	;USE Y AS MEMORY OFFSET AND GRAB STRING.
		LDA	#$00
		STA	COUNT
_MENU1		LDA	MENU_1,Y
		CMP	#$0A	;WAS IT LINEFEED?
		BEQ	_ALLDONE
		STA	ALLMENUS,Y
		INC	COUNT
		INY
		DEX
		CPX	#$FF
		BNE	_MENU1
_ALLDONE	STA	ALLMENUS,Y
		INY
		LDA	#$00
		STA	ALLMENUS,Y	;PUT ZEROS AS END OF STRING MARKER
		JMP	_NOBUTTN
;
;
_VERSION	.TEXT	"WORKSV8.ASM 1-2026 Author: Leslie Newman"
;
_DOSLINK	.WORD	$9EBD
		.WORD	$9E81
;
;
_NOBUTTN	LDA	PWRUP-1		;LOAD POWER UP STATUS
		EOR	#$A5		;
		CMP	PWRUP
		STA	PWRUP
		STA	SAVEPWRUP	;CURIOUS ABOUT THE LAST KEY PRESSED
		BNE 	_DOHOSS		;BRANCH IF NOT A POWER UP
		LDA	READKEY		;CHECK FOR KEYPRESS
		STA	SAVEKEY		;SAVE LAST KEY PRESSED
		LDA	KEYSTROBE	;RESET KEYBOARD STROBE
		LDA	SAVEKEY		;CHECK IF A KEY WAS PRESSED. HIGH BIT CLEAR
		BMI	_DONORM		;NO KEY DOWN, NOT A POWER UP
_DOHOSS		LDX	#$FF
		TXS
		CLD
		JSR	SETNORM		;DO A BIUNCH OF RESET STUFF
		JSR	INIT
		JSR	HOME
		LDA	AN0OFF
		LDA	AN1OFF
		LDY	#5
		JSR	GOTOCX
		LDA	CLRROM
		JSR	_DUMBELL	;FIRST DIFFERENT SPEAKER BEEP
		LDA	SAVEKEY		;WAS A POWER UP IF NO KEY DOWN
		BMI	_NOHELLO
;		JSR	_DUMBELL	;SECOND DIFFERENT SPEAKER BEEP
		LDA 	SAVEKEY
		AND	#$7F		;DON'T CARE ABOUT MSB
		CMP	#$41		;MASK LETTERS INTO CONTROL CODE 
		BCC	_LESS		;BCC    *+4  BRANCH AHEAD 4 BYTES FROM PROG COUNTER
		AND	#$1F
_LESS		TAY			;I CORRECTED THIS LINE 12-18-2025
		CPY	#$03		;C?
		BEQ	_RESET.C	;CATALOG THE DISK IF C KEY DOWN
		LDX	#3
_LINKLP1	LDA	DOSCSWL,X	;IF CSW AND KSW ARE EQUAL
		CMP	CSWL,X		;TO DOSCSW AND DOSKSW
		BNE	_LINKDNE	;ASSUME DOS WAS IN USE
		DEX			;AND RECONNECT DOS
		BPL	_LINKLP1	;THIS ONLY MATTERS IN BOOTLESS
		LDX	#3		;RESETS SUCH AS CONTRPOL-I
_LINKLP2	LDA	_DOSLINK,X	;THE PURPOSE IS TO PREVENT
		STA	CSWL,X		;A CONTROL-I RESET FROM
		DEX			;DISCONNECTING DOS
		BPL	_LINKLP2
_LINKDNE	NOP	
		CPY	#$0D		;M ??
		BEQ	_RESET.M	;GO TO MONITOR
		LDA	#$1B		;NO HELLO FLAG
		PHA
		CPY	#$02		;B ??
		BEQ	_RESET.B	;BOOT ONLY
		PLA
		LDA	#$0		;DO HELLO FLAG
		PHA
		CPY	#$08		;H ??
		BEQ	_RESET.B	;MOVE DOS - WAS _DOMOVE BRANCH
		PLA
_NOHELLO	LDA	#$1B		;NO HELLO FLAG
		PHA
_RESET.B	JSR	_MOVEDOS	;XFER DOS
		PLA			;GET DO HELLO FLAG
		STA	DOSCMDX
		LDA	#<DOSSTRT	;COLD START IS $B73B
		PHA
		LDA	#>DOSSTRT
		PHA
_DONORM		JSR	_SETTEXT
		JMP	_GOMRBRD	;go turn off the Brain Board
;
;
_RESET.C	LDX	#3		;SET CSW AND KSW TO I/O ROUTS
_CATLP		LDA	DOSCSWL,X
		STA	CSWL,X
		DEX
		BPL	_CATLP
		LDA	ONERR		;FIX APPLESOFT ONERR FLAG
		AND	#$7F		;SO DISKI/O ERROR WILL PRINT
		STA	ONERR		;
		LDA	#1		;FIX NO BUFFER FLAG FOR CATALOG
		STA	BFFRFLG
		JSR	_SETTEXT	;Try to intialize the video
		JMP	_GOMRBRD	;go turn off the Brain Board
;
;
;
;
;		JMP $FF59 is handled in the program at $1000
;
_RESET.M		JMP	_GOMRBRD	;go turn off the Brain Board
;
;
;
;		
;_NMISRCE	DW	$3FB
;		DW	$FA62
;
;
;
_DUMBELL	LDY	#$16		;SLIGHTLY DIFFERENT BELL SOUND
_BELP1		LDX	#8
_BELP2		TYA
		JSR	WAIT
		LDA	SPEAKER		;WRITE $C030 LOCATION
		DEX			;
		BNE	_BELP2
		DEY
		BNE	_BELP1
		RTS
;
;
;
;  Move Dos
;
; I changed Sather's code and have everything in the same ROM bank.
;
;		DFB	$FF,$FF,$FF	;Not needed since Move Dos is in the same
;		JSR	_DOSAWAY	;ROM bank.
;		RTS			;All this was from DOS HOSS code
;  
;
;
;
_MOVEDOS	LDA	#0	
 		STA	SRCE
 		STA	DSTN
		LDA	#$9D
		STA	DSTNHI
		LDA	#$D4		;$D400 SHOULD BE START OF DOS IN D000 ROM
		STA	SRCEHI		;I had to pack the DOS from the original
		LDY	#0		;DosHoss ROMs into Bank0 of the Brain Board.
_MOVELP2	LDA	(SRCE),Y
		STA	(DSTN),Y
		INY
		BNE	_MOVELP2
		INC	DSTNHI
		INC	SRCEHI
		LDA	SRCEHI		;I HAD TO CHANGE SATHER'S CODE SINCE I AM PUTTING
					;DOS INTO THE BRAIN BOARD ROM AT DIFFERENT MEMORY
					;LOCATIONS.
		CMP	#$F7		;DOS should end at F6FF THE WAY I AM LOADING FROM ROM.
		BNE	_MOVELP2	;SO DO A COMPARE OF THE SRCEHI TO F7 AND BRANCH UNTIL EQUAL
;		 			;CHECKING USING BCC IF SRCEHI IS LESS THAN OR EQUAL TO #$F7
;		
		LDA	#$60
		STA	RTSTEMP
		JSR	MOVEP3
		LDA	#$A9
		STA	RTSTEMP
		RTS
;
; I added this to try and set the video to normal
; but the menu string that gets printed to the screen
; on DOS load is still mixed inverse and normal characters.
; 
_SETTEXT	LDA	#$FF
		STA	$32
		STA	$C050
		STA	$C053
		STA	$C054
		STA	$C056
		RTS
;
;		
; Now exit to the Apple Motherboard ROM
;
;Now just pulled these next calls from
;the normal Apple Reset at FFFc.FFFd
;
; Starting here we are writing a small assembly routing into memory starting at $1000.
; Remember that once the Brain Board has been switch off and control passed back to the
; Apple nothing on the Brain Board is accessible. So this program is being put into the
; Apple to handle shutting off the Brain Board and finishing the DOS load.
; This program will turn off the Brain Board and then clean up various registers before
; exiting to the screen.
;
;  NOTE: $FB60 CLEARS SCREEN AND PRINTS APPLE ][
;  I ended up not calling this.
;
; 
;
_GOMRBRD	LDA	#$A9
		STA	$1000
		LDA	#-1		;SETUP TO CLEAR THE STACK
		STA	$1001
		LDA	#$9A		;TXS WILL CLEAR THE STACK
		STA	$1002
		LDA	#$AD		;
		STA	$1003
		LDA	HOFFLOW		;D1 = SLOT 5, C1 = SLOT 4
; Should also be able to get the low byte of the Brain Board slot as such:
;  (#$HOSSOFF & $00FF) replaces HOFFLOW
;
; Interesting observation. If you put this card into the wrong slot, on computer boot
; it displays 'HACKIN FOOL!' instead of Apple ][ and it locks up. Seems to be related to
; when I changed the code above from hard coded slot low byte to using HOFFLOW. The assembler
; saw this as doing a LDA zero page instead of LDA absolute.
;
;
		STA	$1004		;CHANGE HOFFLOW EQU VALUE TO SET SLOT
		LDA	#$C0		;LDA $C0XX TO TURN OFF BRAIN BOARD
		STA	$1005		;
		LDA	#$20		;Next I am just doing some Reset stuff
		STA	$1006		;to clean things up after switching from
		LDA	#$20		;the Brain Board ROM.
		STA	$1007
		LDA	#$11		; JSR SETVIDEO
		STA	$1008		; 
;		
;		
;
		LDA	#$D8		;CLD
		STA	$1009
		LDA	#$20
		STA	$100A
		LDA	#$84		;
		STA	$100B
		LDA	#$FE		;
		STA	$100C
		LDA	#$20		;
		STA	$100D
		LDA	#$2F		;
		STA	$100E
		LDA	#$FB		;
		STA	$100F
		LDA	#$20
		STA	$1010
		LDA	#$93
		STA	$1011
		LDA	#$FE
		STA	$1012
		LDA	#$20
		STA	$1013
		LDA	#$89
		STA	$1014
		LDA	#$FE
		STA	$1015
;		
;		
		LDA	#$AD		;LDA ABSOLUTE
		STA	$1016
		LDA	#$01		;LOW BYTE SAVEKEY
		STA	$1017
		LDA	#$20		;HI BYTE SAVEKEY
		STA	$1018
		LDA	#$C9		;CMP IMMEDIATE TO $0D CONTROL + M
		STA	$1019
		LDA	#$0D		;SAVEKEY LOW BYTE
		STA	$101A
		LDA	#$D0		;BNE
		STA	$101B
		LDA	#$06		;JUMP 6 BYTES FORWARD IF NOT EQUAL
		STA	$101C
		LDA	#$20		;JMP TO $FF59 WHICH IS THE MONITOR
		STA	$101D		;DOS DOES NOT LOAD
		LDA	#$EA		;USE CONTROL + B TO EXIT MONITOR AND LOAD DOS
		STA	$101E
		LDA	#$03
		STA	$101F
		LDA	#$4C		;
		STA	$1020
		LDA	#$59  		;
		STA	$1021
		LDA	#$FF
		STA	$1022
;
		LDA	#$20		
		STA	$1034
		LDA	#$84
		STA	$1035
		LDA	#$9D
		STA	$1036
		LDA	#$60  
		STA	$1037
;
;
		LDA	#$AD		;LDA ABSOLUTE
		STA	$1023
		LDA	#$01		;LOW BYTE SAVEKEY
		STA	$1024
		LDA	#$20		;HI BYTE SAVEKEY
		STA	$1025
		LDA	#$C9		;CMP IMMEDIATE TO $03 CONTROL + C
		STA	$1026
		LDA	#$03		;
		STA	$1027
		LDA	#$D0		;BNE
		STA	$1028
		LDA	#$04		;JUMP 4 BYTES FORWARD IF NOT EQUAL
		STA	$1029
		LDA	#$20		;JSR CATALOG
		STA	$102A
		LDA	#$6E
		STA	$102B
		LDA	#$A5
		STA	$102C
		LDA	#$60
		STA	$102D
		LDA	#$EA		;SKIP SETTING TEXT MODE
		STA	$102E
		STA	$102F
		STA	$1030
;		
;		
;
		LDA	#$20
		STA	$1031
;
		LDA	#$00
		STA	$1032
		LDA	#$11
		STA	$1033
		LDA	#$20		;I found I must call $9D84 to re-hook DOS		
		STA	$1034		;after it gets loaded from the Brain Board, but 
		LDA	#$84		;calling $9D84 does not return, so I cannot print
		STA	$1035		;to the screen normal text. If I do not call this the
		LDA	#$9D		;string I print to the screen is normal text, but DOS
		STA	$1036		;does not function. I don't actually need to print
		LDA	#$60		;anything to the screen on DOS load, but thought it would  
		STA	$1037		;be cool.
;
; PRINT MENU 
; Here we write the strings to the screen. Currently just saying DOS is loaded.
; I haven't yet figured out why the text is printing to the screen as mixed flashing,
; inverse and normal.
;
		LDA	#$A0		;LOAD Y
		STA	$1100		;
		LDA	#$00		;WITH $00  
		STA	$1101
		LDA	#$A2		;LOAD X
		STA	$1102
		LDA	#$00		;WITH $00
		STA	$1103
		LDA	#$EA		;NOP
		STA	$1104
		LDA	#$B9		;LDA ABSOLUTE,Y
		STA	$1105
		LDA	#$00		;LOW BYTE OF MENU
		STA	$1106
		LDA	#$21		;HIGH BYTE OF MENU
		STA	$1107
		LDA	#$C9		;CMP
		STA	$1108
		LDA	#$0A		;COMPARE TO $0A
		STA	$1109
		LDA	#$F0		;BEQ IF EQUAL TO RTS		
		STA	$110A
		LDA	#$08		;JUMP FORWARD 8 BYTES
		STA	$110B
;
;
		LDA	#$9D		;STA,X
		STA	$110C
		LDA	#$01		;LOW BYTE COUT
		STA	$110D
		LDA	#$06		;HIGH BYTE COUT
		STA	$110E
		LDA	#$E8		;INX
		STA	$110F
;
;
		LDA	#$C8		;INY
		STA	$1110
		LDA	#$4C		;JMP
		STA	$1111 
		LDA	#$05		;TO $1105 UNTIL $0A END OF STRING
		STA	$1112
		LDA	#$11
		STA	$1113
		LDA	#$60		;RTS
		STA	$1114
;
;
; Here I am trying to initialize the video output
;
SETVIDEO	LDA	#$A9
		STA	$1120
		LDA	#$FF
		STA	$1121
		LDA	#$8D
		STA	$1122
		LDA	#$32
		LDA	$1123
		LDA	#$A9
		STA	$1124
		LDA	#$FF
		STA	$1125
		LDA	#$8D
		STA	$1126
		LDA	#$50
		STA	$1127
		LDA	#$C0
		STA	$1128
		LDA	#$8D
		STA	$1129
		LDA	#$53
		STA	$112A
		LDA	#$C0
		STA	$112B
;
		LDA	#$8D
		STA	$112C
		LDA	#$54
		STA	$112D
		LDA	#$C0
		STA	$112E
;
		LDA	#$8D
		STA	$112F
		LDA	#$56
		STA	$1130
		LDA	#$C0
		STA	$1131
		LDA	#$60		;RTS
		STA	$1132
;
;
		JMP	$1000		;JMP 1000  4C 00 01		
;		
;
STOP		END			;

















